using TASOPT
using TOML
export quicksave_aircraft, quickload_aircraft


"""
    quicksave_aircraft(ac::TASOPT.aircraft=TASOPT.read_aircraft_model(),
        filepath=joinpath(TASOPT.__TASOPTroot__, "IO/IO_samples/default_quicksave_aircraft.toml"))

Converts aircraft `struct` into a dictionary with fields as keys and saves as a toml file.

!!! details "ðŸ”ƒ Inputs and Outputs"
    **Inputs:**
    - `ac::TASOPT.aircraft`: TASOPT aircraft `struct` containing model in any state. 
    - `filepath::String`: path and name of .toml file to be written.
    **Outputs:**
    - None.
"""
function quicksave_aircraft(ac::TASOPT.aircraft=TASOPT.read_aircraft_model(),
    filepath=joinpath(TASOPT.__TASOPTroot__, "IO/IO_samples/default_quicksave_aircraft.toml"))

    #convert structure into dictionary
    acdict = Dict(fieldnames(typeof(ac)) .=> getfield.(Ref(ac), fieldnames(typeof(ac))))

    #check dict values, replace 2d arrays (toml-unsupported)
    # with nested arrays via helper fxn
    acdict_fixed = fix_dict_for_toml(acdict)

    #generate io file
    open(filepath, "w") do io
      #write out aircraft-turned-dictionary
        TOML.print(io,acdict_fixed)
    end
end

"""
    quickload_aircraft(datafile=joinpath(TASOPT.__TASOPTroot__, "IO/IO_samples/default_quicksave_aircraft.toml"))

Reads a .toml file generated by `quicksave_aircraft()` and returns the generated `aircraft` structure.

!!! details "ðŸ”ƒ Inputs and Outputs"
    **Inputs:**
    - `filepath::String`: path and name of .toml file to be written.
    **Outputs:**
    - `ac::TASOPT.aircraft`: TASOPT aircraft `struct` with fields specified by the given .toml file. 
"""
function quickload_aircraft(datafile=joinpath(TASOPT.__TASOPTroot__, "IO/IO_samples/default_quicksave_aircraft.toml"))
    data = TOML.parsefile(datafile)

    @unpack name, description, pari, parg, parm, para, pare, sized = data

    parm = nestedvectors2array(parm) 
    para = nestedvectors2array(para) 
    pare = nestedvectors2array(pare)

    return TASOPT.aircraft(name, description,
        pari, parg, parm, para, pare, sized)
end


#helper fxn: converts matrix into array of arrays
# function slicematrix(A::AbstractMatrix)
#     return [A[i, :] for i in 1:size(A,1)]
# end

"""
    array2nestedvectors(A::Array)

Converts any n-dimensional array to a nested Vector representation and returns it.
Used in .toml IO.
"""
function array2nestedvectors(A::Array)
    if ndims(A) == 1
        # If the array is 1D, return it as is
        return A
    else
        # For higher-dimensional arrays, recursively convert subarrays
        return [array2nestedvectors(copy(subarr)) for subarr in eachslice(A, dims = 1)]
    end
end

"""
    nestedvectors2array(nested_vectors::AbstractVector)

Converts vectors nested within vectors to a multi-dimensional array.
Used in .toml IO.
"""
function nestedvectors2array(nested_vectors::AbstractVector)
    #identify how many levels deep it goes, obtaining lengths
    first_elem = nested_vectors[1]
    lengths = [length(nested_vectors)]
        #while the vector's first element is /still/ a vector
        while typeof(first_elem) <: Vector
            #save length and
            #go a level deeper
            push!(lengths,length(first_elem))
            first_elem = first_elem[1]
        end #once the first element isn't a vector (i.e., reached bottom level)

    #recursively generate flat_vector
    flat_vector = []
    function flatten_vector(v)
        for el in v
            if el isa Vector
                flatten_vector(el)
            else
                push!(flat_vector, el)
            end
        end
    end
    flatten_vector(nested_vectors)

    #reshape flat vector with length information 
    #s.t. output is an Array{Float64,n} where n = length(lengths)
    # result_array = reshape(flat_vector, reverse(lengths)...)
    result_array = permutedims(reshape(flat_vector, reverse(lengths)...),length(lengths):-1:1)

    return result_array
end


"""
    fix_dict_for_toml(dict::Dict)

Prepares a `dict` for output into .toml by:
    - replacing any multi-dimensional arrays with nested arrays (TOML library compatibility restriction),
    - and applying this for any nested `dict`s.
"""
function fix_dict_for_toml(dict::Dict)
    #deep copy of dictionary
    dict = deepcopy(dict)
    for (key, value) in dict
        if isa(value, Array) && ndims(value) >= 2
            dict[key] = array2nestedvectors(value)
        elseif isa(value, Dict)
            dict[key] = fix_dict_for_toml(value)
        end
    end
    return dict
end
